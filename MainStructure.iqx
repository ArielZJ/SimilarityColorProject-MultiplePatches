<defaults>
/ screencolor = (127,127,127)
</defaults>

<include>
/ file = "CircleLocation.iqx"
/ file = "trial_type_controller.iqx"
/ file = "PictureAndText.iqx"
/ file = "ResponseStep.iqx"
/ file = "BaseScript.iqx"
/ file = "8CR_4page.iqx"
/ file = "8CR_disks.iqx"
/ file = "InstructionPage.iqx"
/ file = "PracticeTrial.iqx"
/ file = "calibration.iqx"
/ file = "FollowUp.iqx"
/ file = "Trial_Types.iqx"
</include>

<values>
/trial_number = 0
/Cross_position = 0
/Circle_1 = 0
/Circle_2 = 0
/Circle_3 = 0
/Circle_4 = 0
/Position_1 = 0
/Position_2 = 0
/Position_3 = 0
/Position_4 = 0
/Color_1 = 0
/Color_2 = 0
/Color_3 = 0
/Color_4 = 0
/response_time = 0
/temp = 0

/color1 = #ff0000
/color2 = #ffaa00
/color3 = #aaff00



/color7 = #0000ff
/color8 = #aa00ff

/number_of_patch = 210
/number_of_patch_practice = 9

/patch_timeout = 250
/highlight_timeout = 50

/peripheral_scale = 3

/trial_type = ""
</values>


** modified to add Circle_3 and Circle_4 at Position_3 and Position_4 with Color_3 and Color_4
** added a list to specify the trial types to be chosen out of the mixed trial format
<trial Fixiation>
/ stimulusframes = [1 = cross]
/ timeout = 500
/ recorddata = false
/ ontrialend = [
	values.trial_number += 1;
	values.Circle_1 = 0;
	values.Circle_2 = 0;
	values.Circle_3 = 0;
	values.Circle_4 = 0;
	values.Position_1 = 0;
	values.Position_2 = 0;
	values.Position_3 = 0;
	values.Position_4 = 0;
	values.Color_1 = 0;
	values.Color_2 = 0;
	values.Color_3 = 0;
	values.Color_4 = 0;
	values.response = 0;
	values.trial_type = "";
]
/ branch = [
	if(list.trial_sequence.value == "during"){
		trial.present_during
	} else if(list.trial_sequence.value == "after1"){
		trial.present_after1
	} else if(list.trial_sequence.value == "after2"){
		trial.present_after2
	} else {
		trial.present_catch
	}
]
</trial>

** counter to make sure we do every trial for each trial type
** from 1 to stimuli^2 * condition number (e.g. 3 for FF, FP, PP)
** during
<counter order_during>
/ select = noreplace(1-75)
/ selectionrate = block
</counter>

** after 1
<counter order_after1>
/ select = noreplace(1-50)
/ selectionrate = block
</counter>

** after 2
<counter order_after2>
/ select = noreplace(1-75)
/ selectionrate = block
</counter>

** counters to control the random colours of the other stimuli
<counter rand_color_1>
/ select = noreplace(1-9)
/ resetinterval = 1
</counter>

<counter rand_color_2>
/ select = noreplace(1-9)
/ resetinterval = 1
</counter>

<counter binarise>
/ select = replace(1-2)
/ resetinterval = 1
</counter>


******************************
 Trial types listed in Trial_Types.iqx
******************************



***************
Responses
**************

<trial response_options>
/ stimulusframes = [
	1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank,
highC_instruction, p_response_instructions
]
/ validresponse = (response_blank, response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d)
/ inputdevice = mouse
/ isvalidresponse = [if (trial.response_options.response == "response_blank") false; else
expressions.validresponse_disks
]
/ recorddata = false
/ ontrialend = [
	values.response_time = trial.response_options.latency;
]
/ branch = [
	trial.centermouse
]
</trial>

<trial centermouse>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank,
highC_instruction,centersquare,notice_centre_mouse,process_notice]
/ inputdevice = mouse
/ recorddata = true
/ validresponse = (centersquare)
</trial>


*************************
       Main experiment control
*************************

<block calibration>
	/ trials = [1 = calibration_instruction_page1;]
</block>

<block Instruction>
/ trials = [1 = Instruction_1]
</block>

<block Throughout>
/ trials = [1 = Fixiation]
</block>

<block Throughout_practice>
/ trials = [1 = Fixiation_practice]
</block>


** There should be as many trials in Throughout as there are to collect all the data
** 5 stimuli = 25 trials per condition
** 3 conditions for during & after2 (FF, FP, PP), 2 conditions for after1 (FF, PP) = 200 trials
** 10 catch trials
** 210 trials total in the main block
<expt Main>
/ preinstructions = (start)
/ postinstructions = (end)
/ blocks = [1 = consent; 2 = calibration; 3 = Instruction; 4-12 = Throughout_practice; 13-255 = Throughout; 256 = followup]
</expt>

<data>
/ columns=[subject,values.trial_number, values.trial_type,values.Cross_position,values.Circle_1,values.Position_1,values.Color_1,
values.Circle_2,values.Position_2,values.Color_2,values.Position_3,values.Color_3,values.Position_4,values.Color_4,values.p_confidence,values.response_time,
values.angle_1,values.angle_2,values.angle_3,values.angle_4,values.angle_1_cue,values.angle_2_cue,values.angle_3_cue,values.angle_4_cue]
/ separatefiles = true
</data>